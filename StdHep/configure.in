# ======================================================================
# This is the StdHep configuration input file
# Process this file with autoconf to produce a configure script.
# ======================================================================


# ----------------------------------------------------------------------
# Identify the package and initialize the autotools:
# ----------------------------------------------------------------------

AC_PREREQ(2.57)
AC_INIT(CLHEP StdHep, 2.0.1.0, CLHEP@cern.ch, StdHep)
AC_CONFIG_AUX_DIR(autotools)
AC_CANONICAL_TARGET
AC_CONFIG_SRCDIR([src/collision.cc])
AM_INIT_AUTOMAKE(1.6 foreign)
AC_PROG_LIBTOOL
AC_PROG_INSTALL
AC_PROG_LN_S


# ----------------------------------------------------------------------
# Identify the files that 'configure' is to produce:
# ----------------------------------------------------------------------

# Header containing #defines:
AM_CONFIG_HEADER([StdHep/defs.h])

# Makefiles:
AC_CONFIG_FILES([Makefile])
AC_CONFIG_FILES([StdHep/Makefile])
AC_CONFIG_FILES([StdHep/include/Makefile])
AC_CONFIG_FILES([src/Makefile])
AC_CONFIG_FILES([StdHep-deps])
AC_CONFIG_FILES([StdHep/copy-header.pl], [chmod +x StdHep/copy-header.pl])

# ----------------------------------------------------------------------
# Capture post-install information:
# ----------------------------------------------------------------------

StdHep_CPPFLAGS="-I$includedir"
StdHep_LDFLAGS="-L$libdir"
StdHep_LIBS="-lCLHEP-$PACKAGE-$VERSION -lCLHEP-HepMC-$VERSION -lCLHEP-HepPDT-$VERSION -lCLHEP-Vector-$VERSION"

AC_SUBST(StdHep_CPPFLAGS)
AC_SUBST(StdHep_LIBS)
AC_SUBST(StdHep_LDFLAGS)

AC_SUBST(COPY_P)
AC_SUBST(DIFF_Q)

# ----------------------------------------------------------------------
# Supply boilerplate for StdHep/defs.h source header:
# ----------------------------------------------------------------------

AH_TOP([#ifndef STDHEP_DEFS_H
#define STDHEP_DEFS_H])

## the undefs are converted by configure
AH_VERBATIM([PACKAGE],[/* Name of package */
#ifndef PACKAGE
#undef PACKAGE
#endif])
AH_VERBATIM([PACKAGE_BUGREPORT],[/* Define to the address where bug reports for this package should be sent. */
#ifndef PACKAGE_BUGREPORT
#undef PACKAGE_BUGREPORT
#endif])
AH_VERBATIM([PACKAGE_NAME],[/* Define to the full name of this package. */
#ifndef PACKAGE_NAME
#undef PACKAGE_NAME
#endif])
AH_VERBATIM([PACKAGE_STRING],[/* Define to the full name and version of this package. */
#ifndef PACKAGE_STRING
#undef PACKAGE_STRING
#endif])
AH_VERBATIM([PACKAGE_TARNAME],[/* Define to the one symbol short name of this package. */
#ifndef PACKAGE_TARNAME
#undef PACKAGE_TARNAME
#endif])
AH_VERBATIM([PACKAGE_VERSION],[/* Define to the version of this package. */
#ifndef PACKAGE_VERSION
#undef PACKAGE_VERSION
#endif])
AH_VERBATIM([VERSION],[/* Version number of package */
#ifndef VERSION
#undef VERSION
#endif])

AH_BOTTOM([/*  use correct std::ios_base or std::ios (old) */
#ifdef HAVE_IOS_BASE
#define HepIOS std::ios_base 
#define HepIOSOpenMode std::ios_base::openmode
#define HepIOSBadBit std::ios_base::badbit  
#else 
#define HepIOS std::ios 
#define HepIOSOpenMode std::ios::open_mode
#define HepIOSBadBit std::ios::badbit
#endif

#endif  // STDHEP_DEFS_H])


# ----------------------------------------------------------------------
# Account for any user configuration options:
# ----------------------------------------------------------------------


# ----------------------------------------------------------------------
# Check for needed programs:
# ----------------------------------------------------------------------

# Locate a C++ compiler:
AC_PROG_CXX(cl g++ c++ aCC CC cxx cc++ FCC KCC RCC xlC_r xlC gpp)

# Use it hereinafter:
AC_LANG(C++)

# Ensure we've found a preprocessor:
AC_REQUIRE_CPP

# worry about compiler flags
case "$target" in
*-*-win32*)
  case "$CXX" in
  cl)  CXXFLAGS="-DWIN32 -O -GX -GR -MD";COPY_P="copy -p";DIFF_Q="diff -q -b";;
  g++) CXXFLAGS="-O";COPY_P="copy -p";DIFF_Q="diff -q -b";;
  c++) CXXFLAGS="-O";COPY_P="copy -p";DIFF_Q="diff -q -b";;
  *)   echo UNEXPECTED CHOICE OF C++ COMPILER: $CXX
  esac;;
*-*-cygwin*)
  case "$CXX" in
  cl)  CXXFLAGS="-DWIN32 -O -GX -GR -MD";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  g++) CXXFLAGS="-O -ansi -pedantic -Wall";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  c++) CXXFLAGS="-O -ansi -pedantic -Wall";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  *)   echo UNEXPECTED CHOICE OF C++ COMPILER: $CXX
  esac;;
*-*-solaris*)
  case "$CXX" in
  CC)  CXXFLAGS="-O";COPY_P="cp -p";DIFF_Q="diff -b";;
  g++) CXXFLAGS="-O -ansi -pedantic -Wall";COPY_P="cp -p";DIFF_Q="diff -b";;
  c++) CXXFLAGS="-O -ansi -pedantic -Wall";COPY_P="cp -p";DIFF_Q="diff -b";;
  *)   echo UNEXPECTED CHOICE OF C++ COMPILER: $CXX
  esac;;
*-*-linux*)
  case "$CXX" in
  g++) CXXFLAGS="-O -ansi -pedantic -Wall -D_GNU_SOURCE";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  c++) CXXFLAGS="-O -ansi -pedantic -Wall -D_GNU_SOURCE";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  *)   echo UNEXPECTED CHOICE OF C++ COMPILER: $CXX
  esac;;
alpha*-dec-osf*)
  case "$CXX" in
  cxx) CXXFLAGS="-O -std strict_ansi -timplicit_local";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  g++) CXXFLAGS="-O -ansi -pedantic -Wall";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  c++) CXXFLAGS="-O -ansi -pedantic -Wall";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  *)   echo UNEXPECTED CHOICE OF C++ COMPILER: $CXX
  esac;;
*-*-hpux*)
  case "$CXX" in
  aCC) CXXFLAGS="-O -Aa +DAportable +Onolimit";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  CC)  CXXFLAGS="+O3 +DAportable +a1 -z -pta +Onolimit";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  g++) CXXFLAGS="-O -ansi -pedantic -Wall";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  c++) CXXFLAGS="-O -ansi -pedantic -Wall";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  *)   echo UNEXPECTED CHOICE OF C++ COMPILER: $CXX
  esac;; 
*-*-aix*)
  case "$CXX" in
  xlC) CXXFLAGS="-O3 -qtwolink -+";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  g++) CXXFLAGS="-O -ansi -pedantic -Wall";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  c++) CXXFLAGS="-O -ansi -pedantic -Wall";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  *)   echo UNEXPECTED CHOICE OF C++ COMPILER: $CXX
  esac;; 
*-*-irix*)
  case "$CXX" in
  CC)  CXXFLAGS="-O -OPT:Olimit=0 -pta";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  g++) CXXFLAGS="-O -ansi -pedantic -Wall";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  c++) CXXFLAGS="-O -ansi -pedantic -Wall";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  *)   echo UNEXPECTED CHOICE OF C++ COMPILER: $CXX
  esac;; 
powerpc-apple-darwin*)
  case "$CXX" in
  g++) CXXFLAGS="-O -ansi -pedantic -Wall";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  c++) CXXFLAGS="-O -ansi -pedantic -Wall";COPY_P="cp -p";DIFF_Q="diff -q -b";;
  *)   echo UNEXPECTED CHOICE OF C++ COMPILER: $CXX
  esac;; 
esac


# ----------------------------------------------------------------------
# Set system-dependent options:
# ----------------------------------------------------------------------


# ----------------------------------------------------------------------
# Check for needed libraries:
# ----------------------------------------------------------------------


# ----------------------------------------------------------------------
# Check for needed header files:
# ----------------------------------------------------------------------

AC_CHECK_HEADERS([sstream])

# ----------------------------------------------------------------------
# Check for needed typedefs, structures, and compiler characteristics:
# ----------------------------------------------------------------------

AC_CHECK_TYPES([ptrdiff_t])

AC_MSG_CHECKING([for ios_base])
iosbase=no
AC_COMPILE_IFELSE(
   [AC_LANG_PROGRAM([[#include <fstream>
   std::ios_base::openmode mode;]])],
   [AC_DEFINE([HAVE_IOS_BASE], [], [have std::ios_base]) iosbase=yes] )
AC_MSG_RESULT([$iosbase])

# ----------------------------------------------------------------------
# Check for needed library functions:
# ----------------------------------------------------------------------


# ----------------------------------------------------------------------
# Finish up:
# ----------------------------------------------------------------------

AC_OUTPUT


# ======================================================================
